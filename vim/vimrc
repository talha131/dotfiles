" On Windows, $HOME/.vim is $HOME/vimfiles. For consistency, replace vimfiles with .vim
" See :help vimfiles for more information
if has('win32') && !has('nvim')
     set runtimepath=$HOME/.vim,$VIM/vimfiles,$VIMRUNTIME,$VIM/vimfiles/after,$HOME/.vim/after
endif

" Begin Plugins section                                                       {{{1
call plug#begin('~/.vim/plugged')

if !has('nvim')
    Plug 'tpope/vim-sensible'
endif
Plug 'dahu/bisectly'
Plug 'embear/vim-localvimrc'
Plug 'tpope/vim-speeddating'
Plug 'mzlogin/vim-markdown-toc' " Generate table of contents for Markdown files. `:GenTocGFM`
" Plugins - Code Complete and Snippets                                        {{{2
" The Tab key will, depending on the context, offer Vim's local keyword, file path, omni-completion after typing a period or an arrow operator
" Plug 'ajh17/VimCompletesMe'
" if (has('nvim'))
" Plug 'Shougo/deoplete.nvim', { 'do': ':UpdateRemotePlugins' }
"             \ | Plug 'zchee/deoplete-clang'
"             \ | Plug 'Shougo/neoinclude.vim'
"             \ | Plug 'Shougo/neco-syntax'
"             \ | Plug 'Shougo/neco-vim'
"             \ | Plug 'carlitux/deoplete-ternjs'
" endif
" Plugins - Motion Enhancements                                               {{{2
Plug 'justinmk/vim-sneak' " Use s{char}{char} or S{char}{char}
" Plugins - Session Management                                                {{{2
Plug 'xolox/vim-misc' | Plug 'xolox/vim-session'
" Plugins - Syntax files and Programming Languages                            {{{2
Plug 'Chiel92/vim-autoformat'
Plug 'sheerun/vim-polyglot'
Plug 'ihacklog/HiCursorWords' " highlights words under the cursor like many IDEs
" Plugins - Typing Utilities                                                  {{{2
Plug 'tpope/vim-rsi' " Readline key bindings
Plug 'junegunn/vim-easy-align' " Vim alignment
Plug 'tpope/vim-characterize' " press ga to reveal characters representation in decimal, octal and hex
Plug 'cohama/lexima.vim'
Plug 'tpope/vim-surround' " to make quoting and parenthesizing easier
Plug 'tpope/vim-repeat'
Plug 'tpope/vim-commentary' " toggle comments
Plug 'tpope/vim-endwise'
" Plugins - Distraction Free Writing                                          {{{2
Plug 'junegunn/goyo.vim'
Plug 'junegunn/limelight.vim'
Plug 'chrisbra/NrrwRgn' " focus on a region and make the rest inaccessible. Select and call :NR
" Plugins - Document Outline                                                  {{{2
Plug 'VOoM' " View and change file outline
" Plugins - Utilities and Wrappers                                            {{{2
Plug 'guns/xterm-color-table.vim' " :XtermColorTable to show all 256 xterm colors with their RGB equivalents
Plug 'DrawIt' " Draw ASCII art in Vim
Plug 'tpope/vim-eunuch' " Wrapper for UNIX shell commands
Plug 'henrik/vim-indexed-search' " Show 'Match 123 of 456 <search term>' in Vim searches
Plug 'romainl/vim-qf' " a collection of settings, commands and mappings for the quickfix window
Plug 'romainl/vim-qlist' " make include-search and definition-search populate in the quickfix window
" Plugins - Version Control and File Comparison                               {{{2
Plug 'chrisbra/vim-diff-enhanced' " Enhanced Diff for git diff --patience
" Git wrapper Plug
Plug 'tpope/vim-fugitive'
            \ | Plug 'junegunn/gv.vim'
Plug 'airblade/vim-gitgutter'
Plug 'mbbill/undotree' " Browse the undo tree
" Plugins - Theme                                                             {{{2
Plug 'morhetz/gruvbox'
Plug 'dracula/vim'

" All of your Plugins must be added before the following line
call plug#end()            " required
" End Plugins section }}}

" Custom Functions                                                            {{{1
" from https://www.vi-improved.org/recommendations/
function! StripTrailingWhitespace()
  if !&binary && &filetype != 'diff'
    normal mz
    normal Hmy
    %s/\s\+$//e
    normal 'yz<CR>
    normal `z
  endif
endfunction

" from https://jordanelver.co.uk/blog/2015/05/27/working-with-vim-colorschemes/#showing-highlight-groups
nmap <leader>hg :call <SID>SynStack()<CR>
function! <SID>SynStack()
  if !exists("*synstack")
    return
  endif
  echo map(synstack(line('.'), col('.')), 'synIDattr(v:val, "name")')
endfunction

" Customize Vim                                                               {{{1
if has('nvim')
    " Use <C-L> to clear the highlighting of :set hlsearch. From sensible.vim
    if maparg('<C-L>', 'n') ==# ''
        nnoremap <silent> <C-L> :nohlsearch<C-R>=has('diff')?'<Bar>diffupdate':''<CR><CR><C-L>
    endif
    set inccommand=split
    " Python package for Neovim                                                   {{{2
    let g:python_host_prog = $HOME.'/.pyenv/versions/2.7.10/bin/python'
    let g:python3_host_prog = $HOME.'/.pyenv/versions/3.4.3/bin/python'
endif
" Miscellaneous Variables                                                     {{{2
set expandtab
set guioptions+=b " make sure horizontal scroll bar is visible
set number
set shiftwidth=4
set softtabstop=4
set tabstop=4
set wrap
set ignorecase " Search settings
set hlsearch " Highlight search results
set noerrorbells visualbell t_vb= " Disable Beeping
set rnu " relative number
let g:netrw_bufsettings = 'noma nomod nu nobl nowrap ro rnu' " Except nu and rnu, all are netrw default values
set showcmd

" Spelling                                                                    {{{2
set spellfile=~/.vim/spell/en.utf-8.add " Store all good words in this file
" Generate a binary spell file from text spell file. Vim does it automatically
" whenever a word is added. But when spell file is added externally, you
" need to generate it once
if empty(glob(&spellfile.'.spl'))
    exec 'mkspell!' &spellfile
endif

" Preserve file history                                                       {{{2
set undofile " write undo history to file so that undo history remains persistent
set backup " Make a backup before overwriting a file.  Leave it around after the file has been successfully written.
set writebackup " Make a backup before overwriting a file.  The backup is removed after the file was successfully written, unless the 'backup' option is also on.
set swapfile " Use a swap file for the buffer.

if !isdirectory($HOME.'/.vimTemp')
    silent call mkdir ($HOME.'/.vimTemp', 'p')
endif

set backupdir^=~/.vimTemp// " directory for backup files
set directory^=~/.vimTemp// " directory for swap files
set undodir^=~/.vimTemp// " directory for undo history

" Command Line Completion                                                     {{{2
" from [SO answer](http://stackoverflow.com/a/526940/177116)
" When you type the first tab hit will complete as much as possible, the
" second tab hit will provide a list, the third and subsequent tabs will cycle
" through completion options so you can complete the file without further keys
set wildignorecase
set wildmenu
" Show a list and then choose from the wild menu
set wildmode=list:full

set wildignore+=*.swp,*.bak
set wildignore+=*.pyc,*.class,*.sln,*.Master,*.csproj,*.csproj.user,*.cache,*.dll,*.pdb
set wildignore+=*.png,*.pxm,*.psd,*.pdf,*.jpg,*.gif,*.rtf,*.ico,*.icns,*.tiff
set wildignore+=*.exe,*.bin,*.crl,*.pem,*.crt
set wildignore+=*.plist,*.xcodeproj/**,*.framework/**/*
set wildignore+=*/.git/**/*,*/.hg/**/*,*/.svn/**/*
set wildignore+=tags,cscope.*
set wildignore+=*.tar.*

" Key Mappings                                                                {{{2
" It is better to use <CR> instead  or <C-R> instead of  in key mappings.
" It makes it easier to copy paste, makes the intetion clear that it's a
" single character. Sometimes when copy paste ^M gets translated into enter
" instead of being pasted as a literal
" Use <F7> to toggle spell check in the current buffer
if maparg('<F7>', 'n') ==# ''
  nnoremap <silent> <F7> :setlocal spell!<CR>
endif

" Use <F8> to toggle relative number in the current buffer
if maparg('<F8>', 'n') ==# ''
  nnoremap <silent> <F8> :setlocal relativenumber!<CR>
endif

" Delete buffers and close files without messing up layout
" Why it is needed? See this discussion http://superuser.com/q/289285/42415
command! Bd bp | sp | bn | bd

" Leader Key                                                                  {{{2
" To map to space key, either use ' ' or "\<Space>"
" '<Space>' or '\<Space>' does not work
" For details see http://vi.stackexchange.com/a/282/5643
" :help expr-quote
" Some commands like map understand <Space> but for others you need "\<Space>"
let mapleader = ' '
" git patch write.
" Horizontal Split. Top window is git status.
" Bottom of Horizontal Split has a vertical split.
" Right is for original file. Left is for Git stage.
" When you are in the Git stage split `:wq|$q` closes stage and original file,
" leaving just the Git status split
nnoremap <Leader>pw :wq \| $q<CR>
" Write and quit silently. Useful for quiting Git commit from Vim fugitive
" which always gives following propmt after :wq
" Press ENTER or type command to continue
" See :help hit-enter
nnoremap <Leader>wq :silent wq<CR>
" I want to open Gstatus in a new tab. If I use Gstatus in the current tab or
" window, my file order gets mangled up after I am done with Gstatus.
" tab split = is necessary. Git fugitive commands work only if a file from Git
" repository is open. tab split opens current file in a new tab.
" 0tabmove = moves the tab to the 0th position
" Gstatus = opens Git status
" $q = closes the split that has file from tab split
nnoremap <Leader>gs :tab split \| 0tabmove \| Gstatus \| $q <CR>

" Command Line Completion and find                                            {{{2
" Add folder of current file and every folder under it to Vim's path
set path=.,**
nnoremap <Leader>f :find *
nnoremap <Leader>F :find <C-R>=expand('%:p:h').'/**/*'<CR>
nnoremap <Leader>v :vert sfind *
nnoremap <Leader>V :vert sfind <C-R>=expand('%:p:h').'/**/*'<CR>
nnoremap <Leader>t :tabfind *
" For switching between open buffers
nnoremap <Leader>b :ls<CR>:b<Space>

" Theme Settings                                                              {{{2
if has('mac')
    set guifont=Inconsolata:h14
elseif has('win32')
    set guifont=Inconsolata:h12
endif

function! SetStatusLineColor()
    highlight StatusLine ctermfg=231 ctermbg=61 cterm=bold guifg=#f8f8f2 guibg=#6272a4 gui=bold
    highlight StatusLineNC ctermfg=231 ctermbg=243 cterm=NONE guifg=#f8f8f2 guibg=#64666d gui=NONE
endfunction

function! SetLimelightConcealColor()
    if (&background == 'dark')
        " bg3 from gruvbox dark mode
        let g:limelight_conceal_ctermfg = '241'
        let g:limelight_conceal_guifg = '#665c54'
    else
        " bg3 from gruvbox light mode
        let g:limelight_conceal_ctermfg = '248'
        let g:limelight_conceal_guifg = '#bdae93'
    endif
endfunction

augroup theme_related_settings
    autocmd!
    " Preserve the custom colors set in SetStatusLineColor() whenever
    " colorscheme or background is changed
    autocmd ColorScheme * call SetStatusLineColor()
    autocmd ColorScheme * call SetLimelightConcealColor()
augroup END

set statusline=%F\ %m%r%h%w\ [%L][%p%%][%04l,%04v]\ [%{&ff}]%y\ %{fugitive#statusline()}

set cursorline
set cursorcolumn

colorscheme gruvbox

" Use ag instead of grep                                                      {{{2
if executable("ag")
    set grepprg=ag\ --nogroup\ --nocolor\ --ignore-case\ --column
    set grepformat=%f:%l:%c:%m,%f:%l:%m
endif

" Plugins Customizations                                                      {{{1
" vim-autoformat                                                              {{{2
let g:formatdef_fmt_custom_xml = '"tidy -xml -q --show-errors 0 --show-warnings 0 --indent-attributes 1"'
let g:formatters_xml = ['fmt_custom_xml']
let g:autoformat_verbosemode=1

" Voom                                                                        {{{2
let g:voom_tree_placement = 'right'
let g:voom_ft_modes = {'markdown': 'markdown', 'pandoc': 'markdown'}
let g:voom_always_allow_move_left = 1
augroup plugin_voom
    autocmd!
    autocmd FileType voomtree setlocal nonumber
augroup END

" xolox/vim-session                                                           {{{2
let g:session_autosave = 'yes'
let g:session_autosave_periodic = 5
let g:session_default_overwrite = 1
let g:session_autoload = 'no'

" Limelight and Goyo - Distraction Free Writing                               {{{2
augroup plugin_goyo
    autocmd!
    autocmd User GoyoEnter Limelight
    autocmd User GoyoEnter set nocursorline nocursorcolumn
    autocmd User GoyoLeave Limelight!
    autocmd User GoyoLeave set cursorline cursorcolumn
augroup END

" Vim Easy Align                                                              {{{2
" Start interactive EasyAlign in visual mode (e.g. vipga)
xmap gl <Plug>(EasyAlign)
" Start interactive EasyAlign for a motion/text object (e.g. gaip)
nmap gl <Plug>(EasyAlign)

if (has('nvim'))
" deoplete                                                                    {{{2
let g:deoplete#enable_at_startup = 1
if (has('mac'))
	let g:deoplete#sources#clang#libclang_path = '/Library/Developer/CommandLineTools/usr/lib/libclang.dylib'
	let g:deoplete#sources#clang#clang_header = '/Library/Developer/CommandLineTools/usr/lib/clang'
endif
let g:deoplete#sources#clang#std = {'c': 'c11', 'cpp': 'c++1z', 'objc': 'c11', 'objcpp': 'c++1z'}
let g:deoplete#sources#clang#sort_algo = 'priority'
endif

" gitgutter                                                                   {{{2
set updatetime=200
nnoremap <Leader>hl :GitGutterLineHighlightsToggle <CR>

" undotree                                                                    {{{2
let g:undotree_SplitWidth = 45
let g:undotree_SetFocusWhenToggle = 1

" FileType related settings                                                   {{{1
augroup filetype_related_settings
    autocmd!
    autocmd FileType objc,objcpp,cpp,c setlocal foldmethod=syntax
    autocmd FileType objcpp setlocal cindent
    autocmd FileType gitcommit setlocal colorcolumn=72 " highlight 72nd column in git commit
    if (has('mac'))
        autocmd FileType gitcommit,markdown,text setlocal dictionary=/usr/share/dict/words
    endif
augroup END

" Vim modeline                                                                {{{1
" vim:set foldmethod=marker foldlevel=1:
